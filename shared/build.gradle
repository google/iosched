/*
 * Copyright 2018 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.compileSdkVersion
    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField("String", "CONFERENCE_TIMEZONE", "\"${conference_timezone}\"")
        buildConfigField("String", "PRECONFERENCE_DAY_START", "\"${preconference_day_start}\"")
        buildConfigField("String", "PRECONFERENCE_DAY_END", "\"${preconference_day_end}\"")
        buildConfigField("String", "CONFERENCE_DAY1_START", "\"${conference_day1_start}\"")
        buildConfigField("String", "CONFERENCE_DAY1_END", "\"${conference_day1_end}\"")
        buildConfigField("String", "CONFERENCE_DAY2_START", "\"${conference_day2_start}\"")
        buildConfigField("String", "CONFERENCE_DAY2_END", "\"${conference_day2_end}\"")
        buildConfigField("String", "CONFERENCE_DAY3_START", "\"${conference_day3_start}\"")
        buildConfigField("String", "CONFERENCE_DAY3_END", "\"${conference_day3_end}\"")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        staging {
            initWith debug
        }
    }
    dataBinding {
        enabled true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // UI
    implementation "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support:support-v4:$rootProject.supportLibraryVersion"
    implementation "com.android.support.constraint:constraint-layout:$rootProject.constraintLayoutVersion"

    // Architecture Components
    implementation "android.arch.lifecycle:extensions:$rootProject.archLifecycleVersion"
    implementation "android.arch.lifecycle:livedata:$rootProject.archLifecycleVersion"
    implementation "android.arch.lifecycle:viewmodel:$rootProject.archLifecycleVersion"

    // Utils
    api "com.jakewharton.timber:timber:$rootProject.timberVersion"
    stagingImplementation "com.google.code.gson:gson:$rootProject.gsonVersion"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$rootProject.kotlinVersion"

    // Dagger
    implementation "com.google.dagger:dagger-android:$rootProject.dagger"
    implementation "com.google.dagger:dagger-android-support:$rootProject.dagger"
    kapt "com.google.dagger:dagger-compiler:$rootProject.dagger"
    kapt "com.google.dagger:dagger-android-processor:$rootProject.dagger"

    // ThreeTenBP for the shared module only. Date and time API for Java.
    testImplementation "org.threeten:threetenbp:$rootProject.threetenbpVersion"
    compileOnly "org.threeten:threetenbp:$rootProject.threetenbpVersion:no-tzdb"

    // Unit tests
    testImplementation "android.arch.core:core-testing:$rootProject.archLifecycleVersion"
    testImplementation "junit:junit:$rootProject.junitVersion"

    // Instrumentation tests
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$rootProject.espressoVersion"
    androidTestImplementation "com.android.support.test:runner:$rootProject.runnerVersion"
}
