/*
 * Copyright 2018 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

android {
    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        applicationId "com.google.samples.apps.iosched.tv"
        minSdkVersion rootProject.minTvSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.versionCodeTv
        versionName rootProject.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [crashlyticsEnabled: true]
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            manifestPlaceholders = [crashlyticsEnabled: true]
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        staging {
            initWith debug

            // Specifies a sorted list of fallback build types that the
            // plugin should try to use when a dependency does not include a
            // "staging" build type.
            // Used with :test-shared, which doesn't have a staging variant.
            matchingFallbacks = ['debug']
        }

        debug {
            manifestPlaceholders = [crashlyticsEnabled: false]
        }
    }
    dataBinding {
        enabled true
    }

    lintOptions {
        disable "InvalidPackage", "MissingTranslation"
    }

    // debug and release variants share the same source dir
    sourceSets {
        debug.java.srcDir 'src/debugRelease/java'
        release.java.srcDir 'src/debugRelease/java'
    }
}

dependencies {
    implementation project(":shared")
    testImplementation project(':test-shared')
    testImplementation project(':androidTest-shared')

    // UI
    implementation "androidx.appcompat:appcompat:$rootProject.appcompatVersion"
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.constraintLayoutVersion"
    implementation "androidx.leanback:leanback:$rootProject.leanbackVersion"
    implementation "androidx.tvprovider:tvprovider:$rootProject.tvproviderVersion"
    implementation "com.google.android.material:material:$rootProject.materialVersion"
    implementation "com.google.android:flexbox:$rootProject.flexboxVersion"

    // Architecture Components
    implementation "androidx.lifecycle:lifecycle-extensions:$rootProject.lifecycleVersion"
    kapt "androidx.lifecycle:lifecycle-compiler:$rootProject.lifecycleVersion"
    testImplementation "androidx.arch.core:core-testing:$rootProject.lifecycleVersion"

    // Dagger
    implementation "com.google.dagger:dagger-android:$rootProject.dagger"
    implementation "com.google.dagger:dagger-android-support:$rootProject.dagger"
    kapt "com.google.dagger:dagger-compiler:$rootProject.dagger"
    kapt "com.google.dagger:dagger-android-processor:$rootProject.dagger"

    // Glide
    implementation "com.github.bumptech.glide:glide:$rootProject.glideVersion"
    annotationProcessor "com.github.bumptech.glide:compiler:$rootProject.glideVersion"

    // Utils
    // Date and time API for Java.
    api "com.jakewharton.threetenabp:threetenabp:$rootProject.threetenabpVersion"
    testImplementation "org.threeten:threetenbp:$rootProject.threetenbpVersion"
    implementation "androidx.core:core-ktx:$rootProject.ktxVersion"

    // Fabric and Firebase
    implementation "com.crashlytics.sdk.android:crashlytics:$crashlyticsVersion"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$rootProject.kotlinVersion"

    // Instrumentation tests
    androidTestImplementation "androidx.test.espresso:espresso-core:$rootProject.espressoVersion"
    androidTestImplementation "androidx.test:runner:$rootProject.runnerVersion"

    // Local unit tests
    testImplementation "junit:junit:$rootProject.junitVersion"
    testImplementation "org.hamcrest:hamcrest-library:$rootProject.hamcrestVersion"

    // Solve conflicts with gson. DataBinding is using an old version.
    implementation "com.google.code.gson:gson:$rootProject.gsonVersion"

    // Solve conflicts with okhttp. Firestore is using an old version.
    implementation "com.squareup.okhttp3:okhttp:$rootProject.okhttpVersion"

}

apply plugin: 'com.google.gms.google-services'
