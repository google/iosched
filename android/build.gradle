/*
 * Copyright 2014 Google Inc. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    ext.kotlin_version = '0.8.11'
    repositories {
        maven { url 'http://download.crashlytics.com/maven' }
        mavenCentral()
    }

    dependencies {
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.+'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

lpreview = hasProperty('lpreview')

apply plugin: 'android'
apply plugin: 'crashlytics'
apply plugin: 'kotlin-android'

repositories {
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
    maven { url 'http://download.crashlytics.com/maven' }
}

android {
    compileSdkVersion lpreview ? "android-L" : 19
    buildToolsVersion "19.1.0"

    productFlavors { lpreview ? lpreview{} : classic{} }

    defaultConfig lpreview ? {
//        applicationId "co.touchlab.droidconnyc"
    } : {
        // Non-L-preview configuration
        minSdkVersion 14
        targetSdkVersion 19
//        applicationId "co.touchlab.droidconnyc"
    }

    signingConfigs {
        debug {
            storeFile file("../android/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        release {
            storeFile file("../android/release.jks")
            keyAlias "android"

            storePassword prodPassword
            keyPassword prodPassword

//            storePassword ""
//            keyPassword ""
        }
    }

    buildTypes {
        debug {
            debuggable true
            signingConfig signingConfigs.debug
        }
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), file('proguard-project.txt')
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError false
    }

    aaptOptions {
        noCompress 'apk'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    wearApp project(':Wearable')
    compile 'com.google.android.gms:play-services:5.0.89'
    compile 'com.android.support:support-v13:20.+'
    compile 'com.android.support:support-v4:20.+'
    compile 'com.google.android.apps.dashclock:dashclock-api:+'
    compile 'com.google.code.gson:gson:2.+'
    compile('com.google.api-client:google-api-client:1.+') {
        exclude group: 'xpp3', module: 'shared'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
        exclude group: 'junit', module: 'junit'
        exclude group: 'com.google.android', module: 'android'
    }
    compile 'com.google.api-client:google-api-client-android:1.17.+'
    compile 'com.google.apis:google-api-services-plus:+'
    compile 'com.github.japgolly.android:svg-android:2.0.6'
    compile fileTree(dir: 'libs', include: '*.jar')
    compile files('../third_party/glide/library/libs/glide-3.2.0a.jar')
    compile files('../third_party/basic-http-client/libs/basic-http-client-android-0.88.jar')

    // Google Maps Android API utility library
    compile('com.google.maps.android:android-maps-utils:0.3+') {
        exclude group: "com.google.android.gms"
    }

    compile 'de.greenrobot:eventbus:2.2.1'
    compile 'com.squareup.retrofit:retrofit:1.6.1'
    compile 'com.squareup.picasso:picasso:2.3.3'
    compile 'commons-io:commons-io:2.4'

    /* The two dependencies below are only required while we are using http
       Drive appdata. When we start using AppData through GMS,
       they should be removed. */
//    compile 'com.google.http-client:google-http-client-gson:+'
//    compile 'com.google.apis:google-api-services-drive:+'

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'com.crashlytics.android:crashlytics:1.+'
}
