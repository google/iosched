/*
 * Copyright 2015 Google Inc. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'jacoco'

repositories {
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
    flatDir {
        dirs '../third_party/AndroidSlidingUpPanel/libs/'
    }
    flatDir {
        dirs '../third_party/svg-android/libs/'
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        version "${iosched_version_code}"
        versionName "${iosched_version_name}"
        minSdkVersion 14
        targetSdkVersion 22

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField("boolean", "ENABLE_DEBUG_TOOLS", "false")
        buildConfigField("String", "GCM_API_KEY", "\"${iosched_gcm_api_key}\"")
        buildConfigField("String", "GCM_SENDER_ID", "\"${iosched_gcm_sender_id}\"")
        buildConfigField("String", "GENERIC_API_KEY", "\"${iosched_oauth2_creds_api_key}\"")
        buildConfigField("String", "YOUTUBE_API_KEY", "\"${iosched_youtube_api_key}\"")
        buildConfigField("String", "FEEDBACK_API_ENDPOINT", "\"${iosched_feedback_api_endpoint}\"")
        buildConfigField("String", "TEST_YOUTUBE_LIVE_URL", "\"${iosched_test_youtube_live_url}\"")
        buildConfigField("String", "METADATA_URL", "\"${iosched_metadata_url}\"")
        buildConfigField("String", "WIFI_SSID", "\"${iosched_wifi_ssid}\"")
        buildConfigField("String", "WIFI_PASSPHRASE", "\"${iosched_wifi_passphrase}\"")
        buildConfigField("String", "CONFERENCE_HASHTAG", "\"${iosched_conference_hashtag}\"")
        buildConfigField("String", "CONFERENCE_HASHTAG_PREFIX", "\"${iosched_conference_hashtag_prefix}\"")
        buildConfigField("String", "BOOTSTRAP_DATA_TIMESTAMP", "\"${iosched_bootstrap_data_timestamp}\"")
        buildConfigField("String", "CONFERENCE_DAY1_START", "\"${iosched_conference_day1_start}\"")
        buildConfigField("String", "CONFERENCE_DAY1_END", "\"${iosched_conference_day1_end}\"")
        buildConfigField("String", "CONFERENCE_DAY2_START", "\"${iosched_conference_day2_start}\"")
        buildConfigField("String", "CONFERENCE_DAY2_END", "\"${iosched_conference_day2_end}\"")
        buildConfigField("String", "INPERSON_TIMEZONE", "\"${iosched_inperson_timezone}\"")
        buildConfigField("String", "SHOW_IO15_REQUEST_SOCIAL_PANEL_TIME",
                "\"${iosched_show_io15_request_social_panel_time}\"")
    }

    signingConfigs {
        debug {
            storeFile file("../android/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.debug
            buildConfigField("boolean", "ENABLE_DEBUG_TOOLS", "true")
            buildConfigField("String", "SERVER_MANIFEST_ENDPOINT", "\"${iosched_production_api_manifest_endpoint}\"")
            buildConfigField("String", "GCM_SERVER_URL", "\"${iosched_production_gcm_server_endpoint}\"")
            resValue("string", "website_host_name", "${iosched_production_website_host_name}")
            manifestPlaceholders = [ google_map_key:"${iosched_google_map_api_key}" ]
        }
        qualityassurance {
            debuggable true
            minifyEnabled true
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android.txt'), file('proguard-project.txt')
            buildConfigField("boolean", "ENABLE_DEBUG_TOOLS", "true")
            buildConfigField("String", "SERVER_MANIFEST_ENDPOINT", "\"${iosched_production_api_manifest_endpoint}\"")
            buildConfigField("String", "GCM_SERVER_URL", "\"${iosched_production_gcm_server_endpoint}\"")
            resValue("string", "website_host_name", "${iosched_production_website_host_name}")
            manifestPlaceholders = [ google_map_key:"${iosched_google_map_api_key}" ]
        }
        release {
            debuggable false
            minifyEnabled true
            // No signing config as we do this separately.
            proguardFiles getDefaultProguardFile('proguard-android.txt'), file('proguard-project.txt')
            buildConfigField("String", "SERVER_MANIFEST_ENDPOINT", "\"${iosched_production_api_manifest_endpoint}\"")
            buildConfigField("String", "GCM_SERVER_URL", "\"${iosched_production_gcm_server_endpoint}\"")
            resValue("string", "website_host_name", "${iosched_production_website_host_name}")
            manifestPlaceholders = [ google_map_key:"${iosched_google_map_api_key}" ]
        }
    }

    lintOptions {
        abortOnError false
    }

    aaptOptions {
        noCompress 'apk'
    }

    packagingOptions {
        // Exclude files to avoid packaging collisions (same file name in multiple dependencies).
        exclude 'LICENSE.txt'
    }

    // Test Android jar comes with unimplemented methods that throw exceptions by default. This
    // option forces the methods to return default values instead. Required for static methods,
    // such as TextUtils, as those cannot be mocked with Mockito.
    // Refer http://tools.android.com/tech-docs/unit-testing-support#TOC-Method-...-not-mocked.-
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    // Include libraries checked into the libs directory.
    compile fileTree(dir: 'libs', include: '*.jar')

    // Google Play services split client libs.
    compile "com.google.android.gms:play-services-analytics:${iosched_google_play_services_client_library_version}"
    compile "com.google.android.gms:play-services-drive:${iosched_google_play_services_client_library_version}"
    compile "com.google.android.gms:play-services-maps:${iosched_google_play_services_client_library_version}"
    compile "com.google.android.gms:play-services-plus:${iosched_google_play_services_client_library_version}"
    compile "com.google.android.gms:play-services-wearable:${iosched_google_play_services_client_library_version}"

    // Android support libraries.
    compile "com.android.support:appcompat-v7:${iosched_android_support_lib_version}"
    compile "com.android.support:cardview-v7:${iosched_android_support_lib_version}"
    compile "com.android.support:design:${iosched_android_support_lib_version}"
    compile "com.android.support:support-v13:${iosched_android_support_lib_version}"


    // JSON utility library.
    compile 'com.google.code.gson:gson:2.3'

    // Glide library, used to handle asynchronous loading and caching of images.
    compile 'com.github.bumptech.glide:glide:3.6.1'

    compile files('../third_party/disklrucache/libs/disklrucache-2.0.2.jar')
    compile files('../third_party/volley/libs/volley.jar')
    compile files('../third_party/basic-http-client/libs/basic-http-client-android-0.88.jar')
    // SVG processor for the Conference Map overlay.
    compile (name:'svg-android-2.0.7-20150515.214425-1', ext:'jar')
    // Android Sliding Up Panel library. Compiled from third_party/AndroidSlidingUpPanel.
    // Version number denotes original checked out commit, see README.google in library directory.
    compile (name:'android-sliding-up-panel-master-8129831576', ext:'aar')

    // Google Maps Android API utility library
    compile('com.google.maps.android:android-maps-utils:0.3.4') {
        exclude group: "com.google.android.gms"
    }

    // Dependencies for android tests which reside in the "androidTest" src folder.
    androidTestCompile 'com.android.support.test:runner:0.4.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'

    // Dependencies for unit tests which reside in the "tests" src folder.
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.8'
    testCompile 'org.hamcrest:hamcrest-core:1.3'
    testCompile 'org.json:json:20090211'

    // Added to resolve conflicts between the app build and the test build. The app build resolves
    // the jsr305 dependency to version 1.3.9, but in the test build the espresso-core dependency
    // resolves jsr305 to 2.0.1. Declaring a compile dependency on jsr305 2.0.1 will ensure 2.0.1 or
    // greater is used for both app and test builds.
    compile 'com.google.code.findbugs:jsr305:2.0.1'

    // Needed to implement the IdlingResource interface on certain async models.
    compile 'com.android.support.test.espresso:espresso-idling-resource:2.0'

    compile 'com.google.guava:guava:18.0'
}
