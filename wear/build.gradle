/*
 * Copyright 2018 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.compileWearSdkVersion

    defaultConfig {
        applicationId "com.google.samples.apps.iosched.wear"
        minSdkVersion rootProject.minWearSdkVersion
        targetSdkVersion rootProject.targetWearSdkVersion
        versionCode rootProject.versionCodeWear
        versionName rootProject.versionName
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        staging {
            initWith debug

            // Specifies a sorted list of fallback build types that the
            // plugin should try to use when a dependency does not include a
            // "staging" build type.
            // Used with :test-shared, which doesn't have a staging variant.
            matchingFallbacks = ['debug']
        }
    }

    // debug and release variants share the same source dir
    sourceSets {
        debug.java.srcDir 'src/debugRelease/java'
        release.java.srcDir 'src/debugRelease/java'
    }
    dataBinding {
        enabled = true
    }

    lintOptions {
        disable "BinaryOperationInTimber",
                "LogNotTimber",
                "StringFormatInTimber",
                "ThrowableNotAtBeginning",
                "TimberArgCount",
                "TimberArgTypes",
                "TimberExceptionLogging",
                "TimberTagLength",
                "MissingTranslation"
    }
}

repositories {
    // - Other dependencies
    maven { url "${rootProject.projectDir}/../iosched-prebuilts/m2repository" }
    // - Support Libraries, etc
    maven { url "${rootProject.projectDir}/../../../prebuilts/fullsdk/linux/extras/support/m2repository" }
    maven { url "${rootProject.projectDir}/../../../prebuilts/fullsdk/linux/extras/android/m2repository" }
}

dependencies {
    implementation project(":shared")
    testImplementation project(':test-shared')

    // UI
    implementation "com.android.support:percent:$rootProject.supportLibraryVersion"
    implementation "com.android.support:support-v4:$rootProject.supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support:wear:$rootProject.supportLibraryVersion"
    implementation "com.google.android.support:wearable:$rootProject.wearableSupportLibraryVersion"
    compileOnly "com.google.android.wearable:wearable:$rootProject.wearableSupportLibraryVersion"

    // Architecture Components
    implementation "android.arch.lifecycle:extensions:$rootProject.archLifecycleVersion"
    kapt "android.arch.lifecycle:compiler:$rootProject.archLifecycleVersion"
    testImplementation "android.arch.core:core-testing:$rootProject.archLifecycleVersion"

    // Dagger
    implementation "com.google.dagger:dagger-android:$rootProject.dagger"
    implementation "com.google.dagger:dagger-android-support:$rootProject.dagger"
    kapt "com.google.dagger:dagger-compiler:$rootProject.dagger"
    kapt "com.google.dagger:dagger-android-processor:$rootProject.dagger"

    // Utils
    // Date and time API for Java.
    api "com.jakewharton.threetenabp:threetenabp:$rootProject.threetenabpVersion"
    testImplementation "org.threeten:threetenbp:$rootProject.threetenbpVersion"
    implementation "androidx.core:core-ktx:$rootProject.ktxVersion"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$rootProject.kotlinVersion"

    // Instrumentation tests
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$rootProject.espressoVersion"
    androidTestImplementation "com.android.support.test:runner:$rootProject.runnerVersion"

    // Local unit tests
    testImplementation "junit:junit:$rootProject.junitVersion"
    testImplementation "org.hamcrest:hamcrest-library:$rootProject.hamcrestVersion"

}

// Force Dagger's transitive dependencies to use the project's version of the support libraries.
configurations.all {
    resolutionStrategy {
        eachDependency { details ->
            if (details.requested.group == 'com.android.support') {
                details.useVersion supportLibraryVersion
            }
        }
    }
}
