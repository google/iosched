/*
 * Copyright 2018 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Top-level build file where you can add configuration options common to all
// sub-projects/modules.

buildscript {
    ext {
        // SDK and tools
        compileSdkVersion = 27
        minSdkVersion = 21
        minTvSdkVersion = 21 // TV was introduced with Lollipop, min SDK should be 21.
        targetSdkVersion = 27

        // App dependencies
        constraintLayoutVersion = '1.1.0-beta5'
        espressoVersion = '3.0.1'
        androidGradlePluginVersion = '3.1.0-rc01'
        hamcrestVersion = '1.3'
        junitVersion = '4.12'
        kotlinVersion = '1.2.30'
        runnerVersion = '1.0.1'
        supportLibraryVersion = '27.1.0'
        designLibraryVersion = '27.1.0'
        mdcVersion = '1.0.0-alpha'
        archLifecycleVersion = '1.1.0'
        dataBindingCompilerVersion = '3.0.1'
        threetenabpVersion = "1.0.5"
        threetenbpVersion = "1.3.6"
        timberVersion = "4.6.0"
        dagger = "2.14.1"
        gsonVersion = "2.8.1"
        okhttpVersion = "3.10.0"
        ktxVersion = "0.2"
        googleMapUtilsVersion = "0.5"
        googlePlayServicesClientVersion = "11.8.0"
        googleServicesVersion = "3.2.0"
        firebaseUiVersion = "3.2.2"
    }

    repositories {
        google()
        jcenter()
        // Android Build Server
        maven { url '../iosched-prebuilts/m2repository' }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$androidGradlePluginVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "com.google.gms:google-services:$googleServicesVersion"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()

        // Material Design Components in Kokoro & local
        if (project.hasProperty('mdc_repo_location')) {
            maven { url "$mdc_repo_location" }
        }

        // For Android Build Server
        // - Material Design Components
        maven { url "${rootProject.projectDir}/../iosched-prebuilts/repository" }
        // - Other dependencies
        maven { url "${rootProject.projectDir}/../iosched-prebuilts/m2repository" }
        // - Support Libraries, etc
        maven { url "${rootProject.projectDir}/../../../prebuilts/fullsdk/linux/extras/support/m2repository" }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

// Force all of the primary support libraries to use the same version.
configurations.all {
    resolutionStrategy {
        eachDependency { details ->
            if (details.requested.group == 'com.android.support') {
                details.useVersion supportLibraryVersion
            }
        }
    }
}

