/*
 * Copyright 2018 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Top-level build file where you can add configuration options common to all
// sub-projects/modules.

buildscript {
    ext {
        // App version
        versionName = '2.0.4' // X.Y.Z; X = Major, Y = minor, Z = Patch level
        versionCodeBase = 20400 // XYZZM; M = Module (tv, mobile)
        versionCodeTv = versionCodeBase + 2 // Not used at the moment
        versionCodeMobile = versionCodeBase + 3 // Leave some space for future-proofing

        // SDK and tools
        compileSdkVersion = 29
        minSdkVersion = 21
        targetSdkVersion = 29

        // App dependencies
        aiaVersion = '1.1.0'
        archCoreTestingVersion = '2.1.0-rc01'
        androidGradlePluginVersion = '3.5.0'
        appcompatVersion = '1.1.0-rc01'
        browserVersion = '1.0.0-beta01'
        constraintLayoutVersion = '1.1.2'
        coreVersion = '1.2.0-alpha04'
        coroutinesVersion = '1.3.0'
        crashlyticsVersion = '2.9.4'
        dagger = "2.24"
        dataBindingCompilerVersion = '3.0.1'
        espressoVersion = '3.1.0-alpha1'
        fabricVersion = '1.25.4'
        firebaseAuthVersion = '16.0.4'
        firebaseConfigVersion = '16.0.1'
        firebaseCoreVersion = '16.0.4'
        firebaseFirestoreVersion = '17.1.1'
        firebaseMessagingVersion = '17.3.3'
        firebaseUiVersion = "3.3.1"
        flexboxVersion = "0.3.2"
        fragmentVersion = "1.1.0-rc04"
        glideVersion = "4.9.0"
        googleServicesVersion = "3.2.0"
        gsonVersion = "2.8.1"
        hamcrestVersion = '1.3'
        junitVersion = '4.12'
        kotlinVersion = '1.3.41'
        ktlintVersion = '0.33.0'
        ktxVersion = "1.0.0"
        legacySupportVersion = '1.0.0-alpha1'
        lifecycleVersion = '2.2.0-alpha03'
        materialVersion = '1.1.0-alpha09'
        mockitoVersion = "2.8.9"
        mockitoKotlinVersion = "1.5.0"
        navigationVersion = "2.1.0-rc01"
        okhttpVersion = "3.10.0"
        okioVersion = '1.14.0'
        roomVersion = '2.2.0-rc01'
        rulesVersion = '1.1.1'
        runnerVersion = '1.1.1'
        testExtVersion = '1.1.0'
        threetenabpVersion = "1.0.5"
        threetenbpVersion = "1.3.6"
        timberVersion = "4.7.1"
        workVersion = "2.2.0"
    }

    repositories {
        google()
        mavenCentral()
        jcenter()
        maven {
            url 'https://maven.fabric.io/public'
        }
        // Android Build Server
        maven { url '../adssched-prebuilts/m2repository' }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$androidGradlePluginVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "com.google.gms:google-services:$googleServicesVersion"
        classpath "io.fabric.tools:gradle:$fabricVersion"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$navigationVersion"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id "com.diffplug.gradle.spotless" version "3.24.0"
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()

        // For Android Build Server
        // - Material Design Components
        maven { url "${rootProject.projectDir}/../iosched-prebuilts/repository" }
        // - Other dependencies
        maven { url "${rootProject.projectDir}/../iosched-prebuilts/m2repository" }
        // - Support Libraries, etc
        maven {
            url "${rootProject.projectDir}/../../../prebuilts/fullsdk/linux/extras/support/m2repository"
        }
    }

    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        kotlin {
            target "**/*.kt"
            targetExclude ".idea/"
            ktlint(ktlintVersion)
        }
    }

    afterEvaluate {
        extensions.findByName("kapt")?.arguments {
            arg("room.incremental", "true")
        }
    }
}
